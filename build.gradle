buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.1"
    }
}
apply plugin: "com.gradle.plugin-publish"
apply plugin: 'java-gradle-plugin'
apply plugin: 'groovy'

group 'net.ltgt.gradle'

sourceCompatibility = targetCompatibility = 1.7

gradle.taskGraph.whenReady { graph ->
    if (graph.hasTask(publishPlugins)) {
        assert JavaVersion.current() == JavaVersion.VERSION_1_7 : "Releases must be built with JDK 7 (make sure to clean and rebuild after changing JDK)"

        assert 'git diff --quiet --exit-code'.execute(null, rootDir).waitFor() == 0 : "Working tree is dirty"
        def process ='git describe --exact-match'.execute(null, rootDir)
        assert process.waitFor() == 0 : "Version is not tagged"
        version = process.text.trim() - ~'^v'
    }
}

repositories {
    jcenter()
}
dependencies {
    compile gradleApi()

    testCompile localGroovy()
    testCompile 'com.netflix.nebula:nebula-test:4.0.0'
    testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude group: 'org.codehaus.groovy'
    }
    testCompile gradleTestKit()
}

wrapper {
  gradleVersion = "2.12"
}

test {
    inputs.file tasks.jar
    systemProperty("plugin", tasks.jar.archivePath)
    testLogging {
        showExceptions = true
        showStackTraces = true
        exceptionFormat = 'FULL'
    }
}

pluginBundle {
    website = 'https://github.com/tbroyer/gradle-errorprone-plugin'
    vcsUrl = 'https://github.com/tbroyer/gradle-errorprone-plugin'
    description = 'Gradle plugin to use the error-prone compiler for Java'
    tags = [ 'javac', 'error-prone' ]

    plugins {
        aptPlugin {
            id = 'net.ltgt.errorprone'
            displayName = 'Gradle error-prone plugin'
        }
    }

    mavenCoordinates {
        groupId = project.group
        artifactId = project.name
    }
}

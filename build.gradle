plugins {
    id 'java'
    id 'com.bmuschko.nexus' version '2.0'
}

group 'net.ltgt.gradle'

sourceCompatibility = targetCompatibility = 1.6

dependencies {
    compile gradleApi()
    compile localGroovy()
}

task wrapper(type: Wrapper) {
    gradleVersion = 2.1
}

test {
    testLogging {
        showExceptions = true
        showStackTraces = true
        exceptionFormat = 'FULL'
    }
}

modifyPom {
    project {
        name 'Gradle error-prone plugin'
        description 'Gradle plugin to use the error-prone compiler for Java'
        url 'https://github.com/tbroyer/gradle-errorprone-plugin'
        inceptionYear '2013'

        scm {
            url 'https://github.com/tbroyer/gradle-errorprone-plugin'
            connection 'scm:git:https://github.com/tbroyer/gradle-errorprone-plugin'
            developerConnection 'scm:git:git@github.com:tbroyer/gradle-errorprone-plugin.git'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }

        developers {
            developer {
                name 'Thomas Broyer'
                email 't.broyer@ltgt.net'
            }
        }
    }
}
